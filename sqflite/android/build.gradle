def PLUGIN = "sqflite";
def ANDROIDX_WARNING = "flutterPluginsAndroidXWarning";
gradle.buildFinished { buildResult ->
    if (buildResult.failure && !rootProject.ext.has(ANDROIDX_WARNING)) {
        println '         *********************************************************'
        println 'WARNING: This version of ' + PLUGIN + ' will break your Android build if it or its dependencies aren\'t compatible with AndroidX.'
        println '         See https://goo.gl/CP92wY for more information on the problem and how to fix it.'
        println '         This warning prints for all Android build failures. The real root cause of the error may be unrelated.'
        println '         *********************************************************'
        rootProject.ext.set(ANDROIDX_WARNING, true);
    }
}

group 'com.tekartik.sqflite'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'com.google.dagger:dagger:2.23.1'
    implementation 'com.google.dagger:dagger-android:2.23.1'
    implementation 'com.tencent.wcdb:wcdb-android:1.0.8'
    implementation 'com.github.hurshi.SimplifyDagger:annotation:0.10'
    implementation 'com.github.hurshi.SimplifyDagger:scopes:0.10'
    implementation('com.github.hurshi:daggerobj:0.1') {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }
    annotationProcessor 'com.google.dagger:dagger-compiler:2.23.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.23.1'
    annotationProcessor 'com.github.hurshi.SimplifyDagger:processor:0.10'


}
